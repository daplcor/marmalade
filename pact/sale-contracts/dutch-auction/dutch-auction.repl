(load "../../marmalade.repl")
(begin-tx "Load modules")
  (env-data
    { 'ns: "marmalade-sale"
    , 'upgrade: false })
  (env-sigs [
    { 'key: 'marmalade-user
    ,'caps: []
    }, {
      'key: 'marmalade-contract-admin
    ,'caps: []
    }])
  (load "dutch-auction.pact")
  (marmalade-v2.policy-manager.add-sale-whitelist marmalade-sale.dutch-auction)
(commit-tx)

(typecheck "marmalade-sale.dutch-auction")

(begin-tx "Create and mint a token")
  (use marmalade-v2.ledger)
  (use marmalade-v2.policy-manager)
  (use marmalade-v2.util-v1)
  (use mini-guard-utils)
  (env-hash (hash "create-tokens"))
  (env-chain-data {"chain-id": "0"})
  (env-data {
    "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
   ,"creator-guard":  {"keys": ["creator"], "pred": "keys-all"}
   ,"buyer-guard":  {"keys": ["buyer"], "pred": "keys-all"}
   ,"royalty_spec": {
      "fungible": coin
     ,"creator": "k:creator"
     ,"creator-guard":  {"keys": ["creator"], "pred": "keys-all"}
     ,"royalty-rate": 0.1
    }
  })

  (test-capability (coin.COINBASE))
  (coin.coinbase "k:creator" (read-keyset 'creator-guard) 250.0)
  (coin.coinbase "k:buyer" (read-keyset 'buyer-guard) 250.0)
  ;  (coin.coinbase "k:next-buyer" (read-keyset 'next-buyer-guard) 250.0)

  (expect "Create the token"
    true
    (create-token (read-string "token1-id") 0 "dutch-auction-uri-1" (create-policies DEFAULT_ROYALTY) ALWAYS-TRUE))

  (env-sigs [
    { 'key: 'creator
    ,'caps: [
      (marmalade-v2.ledger.MINT (read-msg 'token1-id) "k:creator" 1.0)
    ]
    }])

  (expect "Mint the token"
    true
    (mint (read-string "token1-id" ) "k:creator" (read-keyset 'creator-guard ) 1.0))
(commit-tx)

 (begin-tx "Offer token for sale with the dutch auction sale type and a non-finalized price")
   (env-hash (hash "offer-token-1"))
   (use marmalade-v2.ledger)
   (use marmalade-v2.policy-manager)
   (use marmalade-v2.util-v1)
   (use mini-guard-utils)

   (env-data {
     "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
     ,"quote":{
       "fungible": coin
       ,"sale-price": 0.0
       ,"seller-fungible-account": {
           "account": "k:creator"
         ,"guard": {"keys": ["creator"], "pred": "keys-all"}
       }
       ,"sale-type": "marmalade-sale.dutch-auction"
      }
     })

   (env-sigs [
     { 'key: 'creator
      ,'caps: [
       (marmalade-v2.ledger.OFFER (read-msg 'token1-id) "k:creator" 1.0 0)]
      }])

   (expect "Offer token up for sale"
     "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
     (sale (read-msg 'token1-id) "k:creator" 1.0 0))
 (commit-tx)


 (begin-tx "Create dutch auction")
   (env-hash (hash "create-dutch-auction"))
   (env-chain-data {"block-time": (time "2023-10-01T00:00:00Z")})
   (use marmalade-v2.ledger)
   (use marmalade-v2.util-v1)
   (use mini-guard-utils)
   (use marmalade-sale.dutch-auction)

   (env-data {
     "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
    ,"sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
    ,"creator-guard":  {"keys": ["creator"], "pred": "keys-all"}
   })

   (expect-failure "Creating a ducth auction in the past fails"
     "Start date must be in the future"
     (create-auction (read-string "sale-id") (read-string "token1-id") 1696104800 1696723200 50.0 100.0)
   )

   (expect-failure "Creating a dutch auction with end-date before startdate fails"
     "End date must be after start date"
     (create-auction (read-string "sale-id") (read-string "token1-id") 1696204800 1696204700 50.0 100.0)
   )

   (expect-failure "Creating a dutch auction with reserve-price below start-price fails"
     "Start price must be greater than reserve price"
     (create-auction (read-string "sale-id") (read-string "token1-id") 1696204800 1696723200 100.0 50.0)
   )

   (env-sigs [
    { 'key: 'creator
     ,'caps: [(MANAGE_AUCTION (read-msg 'sale-id) (read-msg 'token1-id))]
     }])

   (expect "Create a dutch auction"
     true
     (create-auction (read-string "sale-id") (read-string "token1-id") 1696204800 1696723200 50.0 100.0)
   )

   (expect "Auction info stored with identifier 1"
     { "token-id": (read-string "token1-id")
       ,"start-date": 1696204800
       ,"end-date": 1696723200
       ,"start-price": 100.0
       ,"reserve-price": 50.0
       ,"sell-price": 0.0
       ,"buyer": ""
       ,"buyer-guard": marmalade-sale.dutch-auction.DUMMY_GUARD
     }
     (retrieve-auction (read-string "sale-id"))
   )

   (expect "create-auction events"
     [
       {"name": "marmalade-sale.dutch-auction.AUCTION_CREATED","params": ["_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y" "t:VP0cvrlSaYJ3HizKnl3g0eL9aDr_RQhunuYgI4BTTe0"]}
     ]
     (map (remove "module-hash")  (env-events true))
   )
 (commit-tx)

 (begin-tx "Update auction")
   (env-hash (hash "update-auction"))
   (use marmalade-v2.ledger)
   (use marmalade-v2.util-v1)
   (use mini-guard-utils)
   (use marmalade-sale.dutch-auction)

   (env-data {
     "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
    ,"sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
   })
   (env-sigs [])
   (expect-failure "Update auction without rights fails"
     "Keyset failure"
     (update-auction (read-string "sale-id") 1696809600 1697328000 100.0 50.0)
   )
   (env-sigs [
     { 'key: 'creator
      ,'caps: [(MANAGE_AUCTION (read-msg 'sale-id) (read-msg 'token1-id))]
      }
   ])
   (expect "Update auction succeeds"
     "Write succeeded"
     (update-auction (read-string "sale-id") 1696809600 1697328000 110.0 50.0)
   )
 (rollback-tx)

 (begin-tx "Update auction failure")
   (env-hash (hash "update-auction-failure"))
   (env-chain-data {"block-time": (time "2023-10-03T00:00:00Z")})
   (use marmalade-v2.ledger)
   (use marmalade-v2.util-v1)
   (use mini-guard-utils)
   (use marmalade-sale.dutch-auction)

   (env-data {
     "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
    ,"sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
   })
   (env-sigs [
     { 'key: 'creator
      ,'caps: [(MANAGE_AUCTION (read-msg 'sale-id) (read-msg 'token1-id))]
      }
   ])
   (expect-failure "Update auction after it has started fails"
     "Can't update auction after it has started"
     (update-auction (read-string "sale-id") 1696809600 1697328000 110.0 50.0)
   )
 (rollback-tx)

 (begin-tx "Withdrawal before auction start")
   (env-hash (hash "bid-withdrawal-before-start"))
   (env-chain-data {"block-time": (time "2023-10-01T00:00:00Z")})
   (use marmalade-v2.ledger)

   (env-data {
     "sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
   })

   (expect-failure "withdrawing before the auction has ended fails"
     "Auction is still ongoing or hasn't started yet"
     (continue-pact 0 true (read-msg 'sale-id))
   )
 (rollback-tx)

 (begin-tx "Withdrawal after auction end")
   (env-hash (hash "bid-withdrawal-after-end"))
   (env-chain-data {"block-time": (time "2023-10-10T00:00:00Z")})
   (use marmalade-v2.ledger)
   (use marmalade-v2.util-v1)
   (use mini-guard-utils)

   (env-data {
     "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
     ,"sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
   })

   (env-sigs [
     { 'key: 'creator
      ,'caps: [(marmalade-v2.ledger.WITHDRAW (read-msg 'token1-id) "k:creator" 1.0 0 (read-msg 'sale-id))]
      }
   ])

   (expect "withdrawing after the auction has ended withoud a price accepted succeeds"
     "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
     (continue-pact 0 true (read-msg 'sale-id))
   )
 (rollback-tx)

 (begin-tx "Auction price validations")
  (env-hash (hash "auction-price-validations"))
  (use marmalade-v2.ledger)
  (use marmalade-v2.util-v1)
  (use mini-guard-utils)
  (use marmalade-sale.dutch-auction)

  (env-data {
    "token1-id": (create-token-id { 'uri: "dutch-auction-uri-1", 'precision: 0, 'policies: (create-policies DEFAULT_ROYALTY)} ALWAYS-TRUE)
    ,"sale-id": "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
  })

  (env-chain-data {"block-time": (time "2023-10-01T00:00:00Z")})
  (expect "Dutch auction current price returns 0 before the auction has started"
    0.0
    (get-current-price (read-string 'sale-id))
  )

  (env-chain-data {"block-time": (time "2023-10-02T00:00:00Z")})
  (expect "Current price returns opening price at start"
    100.0
    (get-current-price (read-string 'sale-id))
  )

  (env-chain-data {"block-time": (time "2023-10-02T00:31:00Z")})
  (expect "Current price drops as time progresses"
    99.65
    (get-current-price (read-string 'sale-id))
  )

  (env-chain-data {"block-time": (time "2023-10-06T00:00:00Z")})
  (expect "Current price drops as time progresses"
    66.67
    (get-current-price (read-string 'sale-id))
  )

  (env-chain-data {"block-time": (time "2023-10-07T23:45:00Z")})
  (expect "Just before closing the price is at reserve price"
    50.00
    (get-current-price (read-string 'sale-id))
  )
(rollback-tx)

 (begin-tx "Buying the token with the wrong price fails")
   (env-hash (hash "dutch-auction-price-failure"))
   (env-chain-data {"block-time": (time "2023-10-07T00:00:00Z")})
   (use marmalade-v2.ledger)
   (use marmalade-sale.dutch-auction)

   (env-data {
     'buyer: "k:buyer",
     'buyer-guard: {"keys": ["buyer"], "pred": "keys-all"},
     'buyer_fungible_account: "k:buyer"
     ,'updated_price: 25.0
     })

   (expect-failure "Try to claim the token with the wrong price"
     "Price does not match current price"
     (continue-pact 1 false "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y")
   )
 (rollback-tx)

 (begin-tx "Buying the token at current price succeeds")
   (env-hash (hash "dutch-auction-claim-success"))
   (env-chain-data {"block-time": (time "2023-10-06T00:00:00Z")})
   (use marmalade-v2.ledger)
   (use marmalade-v2.policy-manager)
   (use marmalade-sale.dutch-auction)

   (env-data {
     'buyer: "k:buyer",
     'buyer-guard: {"keys": ["buyer"], "pred": "keys-all"},
     'buyer_fungible_account: "k:buyer",
     'policy-manager-escrow: (at 'account (get-escrow-account "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y")),
     'updated_price: 66.67
     })

   (env-sigs [
     { 'key: 'any
      ,'caps: [(marmalade-v2.ledger.BUY "t:VP0cvrlSaYJ3HizKnl3g0eL9aDr_RQhunuYgI4BTTe0" "k:creator" "k:buyer" 1.0 "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y")]
      },
      { 'key: 'buyer
       ,'caps: [(coin.TRANSFER "k:buyer" (read-string 'policy-manager-escrow) 66.67)]
       }])

   (expect "Claim the token succeeds"
     "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"
     (continue-pact 1 false "_vS1Y4nXQavtHxQAUCNDeRzG5Jc8Se22Ocg4RNP5B2Y"))

   (expect "Buyer account balance has been deducted"
     183.33
     (coin.get-balance "k:buyer")
   )

   (expect "Seller balance has increased"
     316.67
     (coin.get-balance "k:creator")
   )
 (commit-tx)
